plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id "com.github.johnrengelman.shadow" version "7.0.0"
	id 'maven-publish'
}

apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	shade
}

repositories {
}

String[] apiModules = [
		"fabric-resource-loader-v0"
]

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	apiModules.each {
		modApi include(fabricApi.module(it, project.fabric_version))
	}

	shade implementation('io.github.vincenzopalazzo:material-ui-swing:1.1.2')
	shade implementation ('org.sharegov:mjson:1.4.1') {
		transitive false
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	it.options.release = 16
}

java {
	withSourcesJar()
}

jar {
	manifest {
		attributes("Implementation-Title": project.name,
				"Implementation-Version": project.version,
				"Main-Class": 'it.multicoredev.opentoall.installer.Installer')
	}

	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
	exclude('**/*.psd')
}

shadowJar {
	configurations = [project.configurations.shade]
	from sourceSets.main.allSource
	classifier ''

	minimize()
	archiveClassifier.set(null)
	exclude('**/*.psd')
}


// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
	repositories {
	}
}
